plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'com.android.library'
apply plugin: 'android-apt'
apply plugin: 'maven-publish'

ext {
    bintrayRepo = 'mp4maker'
    bintrayName = 'mp4-maker'

    publishedGroupId = 'com.mp4maker.mp4maker'
    libraryName = 'Mp4Maker'
    artifact = 'mp4-maker'

    libraryDescription = 'mp4 maker lib'

    siteUrl = 'https://github.com/xiaogegexiao'
    gitUrl = 'https://github.com/xiaogegexiao'

    libraryVersion = '1.1.0'

    developerId = 'xiao'
    developerName = 'xiao'
    developerEmail = 'xiaogegexiao@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'
    compile 'com.squareup.dagger:dagger:1.2.5'
    apt 'com.squareup.dagger:dagger-compiler:1.2.5'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.trello.rxlifecycle2:rxlifecycle:2.0.1'
    compile 'com.trello.rxlifecycle2:rxlifecycle-components:2.0.1'
}

//Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')
    publications = ['MyPublication']

    pkg {
        repo = bintrayRepo
        name = bintrayName
        userOrg = properties.getProperty('bintray.user')
        licenses = allLicenses
        vcsUrl = gitUrl

        version {
            name = libraryVersion
            desc = libraryDescription
            released  = new Date()
            vcsTag = libraryVersion
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty("bintray.gpg.password")
                //Optional. The passphrase for GPG signing'
            }
        }
    }
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name licenseName
            url licenseUrl
            distribution "repo"
        }
    }
    developers {
        developer {
            id developerId
            name developerName
            email developerEmail
        }
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId publishedGroupId
            artifactId artifact
            version libraryVersion

//            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/library-release.aar")

            pom.withXml {
                def root = asNode()
                root.appendNode('description', libraryDescription)
                root.appendNode('name', libraryName)
                root.appendNode('url', siteUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

// Place it at the end of the file
//apply from: 'https://raw.githubusercontent.com/xiaogegexiao/JCenter/master/bintrayv1.gradle'
//apply from: 'https://raw.githubusercontent.com/xiaogegexiao/JCenter/master/installv1.gradle'
